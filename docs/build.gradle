buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.0'
        classpath 'org.ajoberstar:gradle-git-publish:0.3.2'
    }
}

apply plugin: 'org.asciidoctor.gradle.asciidoctor'
apply plugin: 'org.ajoberstar.git-publish'

gitPublish {
    repoUri = 'git@github.com:longwa/build-test-data.git'
    branch  = 'gh-pages'

    // what to publish, this is a standard CopySpec
    contents {
        from "${buildDir}/docs"
    }
}

asciidoctor {
    outputDir = new File("$buildDir/docs")
    options = [
        doctype: 'book',
        attributes: [
            'source-highlighter': 'coderay',
            toc                 : 'left',
            toclevels           : '2',
            'toc-title'         : 'Table of Contents',
            icons               : 'font',
            id                  : project.name + ':' + project.version,
            idprefix            : '',
            idseparator         : '-',
            version             : version
        ]
    ]
}

// Rebuild docs before pushing
gitPublishPush.dependsOn asciidoctor

